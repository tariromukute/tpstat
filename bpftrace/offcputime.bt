#!/usr/local/bin/bpftrace

#include <linux/sched.h>

/* This flame graph is dominated by threads sleeping, waiting for work. Applications of interest can be examined by clicking their names to zoom in. */

BEGIN
{
        printf("Tracing nanosecond time in off-CPU stacks. Ctrl-C to end.\n");
        @begin = nsecs;
}

kprobe:finish_task_switch
{
        // record previous thread sleep time
        $prev = (struct task_struct *)arg0;
        if ($1 == 0 || $prev->tgid == $1) {
                @start[$prev->pid] = nsecs;
        }

        // get the current thread start time
        $last = @start[tid];
        if ($last != 0) {
                @[kstack, ustack, comm] = sum(nsecs - $last);
                delete(@start[tid]);
        }
}

interval:s:5
{
        print(@);
        clear(@);  
}

END
{
        clear(@start);
        printf("Duration %d ms \n", (nsecs - @begin)/1000000);
}