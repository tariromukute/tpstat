#!/usr/local/bin/bpftrace

/**
 * shows the size of received and sent packets from the net device layer, both before and after software segmentation  * offload (GSO and GRO).
 */
BEGIN
{
        printf("Tracing net device send/receive. Hit Ctrl-C to end.\n");
        @begin = nsecs;
}

tracepoint:net:netif_receive_skb
{
        @recv_bytes[str(args->name)] = hist(args->len);
}

tracepoint:net:net_dev_queue
{
        @send_bytes[str(args->name)] = hist(args->len);
}

tracepoint:net:napi_gro_receive_entry
{
        @nic_recv_bytes[str(args->name)] = hist(args->len);
}

tracepoint:net:net_dev_xmit
{
        @nic_send_bytes[str(args->name)] = hist(args->len);
}

interval:s:5
{
       print(@nic_send_bytes);
       clear(@nic_send_bytes);

       print(@nic_recv_bytes);
       clear(@nic_recv_bytes);

       print(@send_bytes);
       clear(@send_bytes);

       print(@recv_bytes);
       clear(@recv_bytes);
}

END
{
    printf("Duration %d ms \n", (nsecs - @begin)/1000000);
}