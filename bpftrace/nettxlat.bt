#!/usr/local/bin/bpftrace

/**
 * shows network device transmission latency: the time spent pushing the packet into the driver layer to enqueue it on
 * a TX ring for the hardware to send out, until the hardware signals the kernel that packet transmission has completed * (usually via NAPI) and the packet is freed.
 */
BEGIN
{
        printf("Tracing net device xmit queue latency. Hit Ctrl-C to end.\n");
        @begin = nsecs;
}

/* Issues packet to the device queue */
tracepoint:net:net_dev_start_xmit
{
        @start[args->skbaddr] = nsecs;
}

/* Frees the packet which occurs when the device has completed sending it */
/* The skb:kfree_skb tracepoint is a counterpart of skb:consume_skb. The consume_skb tracepoint fires for the normal skb consumption code path, and kfree_skb fires for other unusual events that may be worth investigating. */
tracepoint:skb:consume_skb
/@start[args->skbaddr]/
{
        @us = hist((nsecs - @start[args->skbaddr]) / 1000);
        delete(@start[args->skbaddr]);
}

/* Some times the packets don't pass through tracepoint:skb:consume_skb so delete to avoid reuse */
tracepoint:net:net_dev_queue
{
        // avoid timestamp reuse:
        delete(@start[args->skbaddr]);
}

interval:s:5
{
        print(@us);
        clear(@us);  
}

END
{
        clear(@start);
        printf("Duration %d ms \n", (nsecs - @begin)/1000000);
}